
#type compute
#version 450

#include <hdr_common.glsl>

#include <renderer_transform_buffers.glsl>
#include <gbuffer.glsl>

layout(local_size_x = bins / 2, local_size_y = 1) in;

layout(std430, binding = 0) restrict writeonly buffer histogram_sums {
	uint sums[bins];
};
layout(std430, binding = 1) restrict readonly buffer histogram_bins {
	uint histogram[bins];
};
layout(std430, binding = 2) restrict writeonly buffer hdr_bokeh_parameters_buffer {
	hdr_bokeh_parameters params;
};

layout(push_constant) uniform hdr_lum_resolution_t {
	uint hdr_lum_resolution;
};

shared uint shared_data[bins];

void main() {
	uint id = gl_LocalInvocationID.x;

	float N = fbins;
	float T = 1.25f / (N);
	int bin_ceil = int(hdr_lum_resolution * T);

	for (int j=0; j<2; ++j) {
		int h = int(histogram[id * 2 + j]);

		int trim = 0;//max(0, h - bin_ceil);
		shared_data[id * 2 + j] = uint(h - trim);
	}

	memoryBarrierShared();
	barrier();

	const uint steps = uint(log2(fbins) + 1);
	for (int i=0; i<steps; ++i) {
		uint mask = (1 << i) - 1;
		uint read_idx = ((id >> i) << (i + 1)) + mask;
		uint write_idx = read_idx + 1 + (id & mask);

		shared_data[write_idx] += shared_data[read_idx];

		memoryBarrierShared();
		barrier();
	}

	sums[id * 2] = shared_data[id * 2];
	sums[id * 2 + 1] = shared_data[id * 2 + 1];

	if (id == 0) {
		float d = texelFetch(depth_map, textureSize(depth_map, 0) >> 1, 0).x;
		float z_lin = unproject_depth(d);

		params.focus = z_lin;
	}
}
